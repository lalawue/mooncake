--
-- Copyright (c) 2021 lalawue
--
-- This library is free software; you can redistribute it and/or modify it
-- under the terms of the MIT license. See LICENSE for details.
--

import parse from "mnscript.parser" {}
import compile from "mnscript.compile" {}
import split, posLine from "mnscript.utils" {}
import concat, insert, remove from table {}

unpack = unpack or table.unpack

-- mn source to AST
fn toAST(config, text) {
    t = type(text)
    if t ~= "string" {
        return nil, "expecting string (got " .. t .. ")"
    }
    config = config or {}
    ret, tbl = parse(text)
    if not ret {
        err = posLine(tbl.content, tbl.lpos, tbl.cpos)
        msg = string.format("parse error %s:%d: %s", config.fname, err.line, err.message)
        return nil, msg
    }
    return tbl
}

-- translate to Lua
fn toLua(config, tbl) {
    ret, code = compile(config, tbl)
    if not ret {
        return nil, code
    }
    return code
}

-- directory separator
dir_spliter = package.config and package.config[1] or '/'
all_option = _VERSION == "Lua 5.1" and "*a" or "a"
tmp_config = {}

-- register ?.mn loader
fn mnLoader(name) {
    name_path = name:gsub("%.", dir_spliter)
    file, file_path = nil, nil
    for path in package.path:gmatch("[^;]+") {
        len = path:len()
        path = path:sub(1, len - 4) .. ".mn"
        file_path = path:gsub("?", name_path)
        file = io.open(file_path)
        if file {
            break
        }
    }
    text = nil
    if file {
        text = file:read(all_option)
        file:close()
    } else {
        return nil, "Could not find mn file"
    }
    tmp_config.fname = file_path
    res, emsg = toAST(tmp_config, text)
    if not res {
        error(emsg)
    }
    res, emsg = toLua(tmp_config, res)
    if not res {
        error(emsg)
    }
    f, err = load(res, file_path)
    return f
}

fn mnLoadString(text, cname, mode, env) {
    tmp_config.fname = cname
    res, emsg = toAST(tmp_config, text)
    if not res {
        return emsg
    }
    res, emsg = toLua(tmp_config, res)
    if not res {
        return emsg
    }
    f = (loadstring or load)
    return f(res, cname, unpack({ mode : mode, env : env }))
}

fn mnLoadFile(fname, ...) {
    f, err = io.open(fname)
    if not f {
      return nil, err
    }
    local text = assert(f:read(all_option))
    f:close()
    return mnLoadString(text, fname, ...)
}

fn mnDoFile(...) {
    f = assert(mnLoadFile(...))
    return f()
}

local _mn_loaded = false

fn mnRemoveLoader() {
    if not _mn_loaded {
        return
    }
    loaders = package.loaders or package.searchers
    for i, loader in ipairs(loaders) {
        if loader == mnLoader {
            remove(loaders, i)
            _mn_loaded = false
            return true
        }
    }
    return false    
}

fn mnAppendLoader() {
    if _mn_loaded {
        return
    }
    loaders = package.loaders or package.searchers
    has_loader = false
    for i=1, #loaders {
        if loaders[i] == mnLoader {
            has_loader = true
        }
    }
    if not has_loader {
        _mn_loaded = true
        insert(loaders, mnLoader)
    }
}

fn mnVersion() {
    return "mnscript v0.3.20210601, " .. _VERSION
}

fn mnLoaded() {
    return _mn_loaded
}

-- append loader
mnAppendLoader()

return {
    loadstring : mnLoadString,
    loadfile : mnLoadFile,
    dofile : mnDoFile,
    removeloader : mnRemoveLoader,
    appendloader : mnAppendLoader,
    toAST : toAST,
    toLua : toLua,
    version : mnVersion,
    loaded : mnLoaded
}