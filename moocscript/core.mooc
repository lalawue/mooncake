--
-- Copyright (c) 2021 lalawue
--
-- This library is free software; you can redistribute it and/or modify it
-- under the terms of the MIT license. See LICENSE for details.
--

import parse from "moocscript.parser" {}
import compile, clearproj from "moocscript.compile" {}
import split, posLine from "moocscript.utils" {}
import concat, insert, remove from table {}

unpack = unpack or table.unpack

-- source to AST
fn toAST(config, text) {
    t = type(text)
    if t ~= "string" {
        return nil, "expecting string (got " .. t .. ")"
    }
    config = config or {}
    ret, tbl = parse(text)
    if not ret {
        err = posLine(tbl.content, tbl.lpos, tbl.cpos)
        msg = string.format("parse error %s:%d: %s", config.fname, err.line, err.message)
        return nil, msg
    }
    return tbl
}

-- translate to Lua
fn toLua(config, tbl) {
    ret, code = compile(config, tbl)
    if not ret {
        return nil, code
    }
    return code
}

-- directory separator
dir_spliter = package.config and package.config[1] or '/'
all_option = _VERSION == "Lua 5.1" and "*a" or "a"
tmp_config = {}

-- register loader
fn mcLoader(name) {
    name_path = name:gsub("%.", dir_spliter)
    file, file_path = nil, nil
    for path in package.path:gmatch("[^;]+") {
        len = path:len()
        path = path:sub(1, len - 4) .. ".mooc"
        file_path = path:gsub("?", name_path)
        file = io.open(file_path)
        if file {
            break
        }
    }
    text = nil
    if file {
        text = file:read(all_option)
        file:close()
    } else {
        return nil, "Could not find .mooc file"
    }
    tmp_config.fname = file_path
    res, emsg = toAST(tmp_config, text)
    if not res {
        error(emsg)
    }
    res, emsg = toLua(tmp_config, res)
    if not res {
        error(emsg)
    }
    f, err = load(res, file_path)
    return f
}

fn mcLoadString(text, cname, mode, env) {
    tmp_config.fname = cname
    res, emsg = toAST(tmp_config, text)
    if not res {
        return emsg
    }
    res, emsg = toLua(tmp_config, res)
    if not res {
        return emsg
    }
    f = (loadstring or load)
    return f(res, cname, unpack({ mode : mode, env : env }))
}

fn mcLoadFile(fname, ...) {
    f, err = io.open(fname)
    if not f {
      return nil, err
    }
    local text = assert(f:read(all_option))
    f:close()
    return mcLoadString(text, fname, ...)
}

fn mcDoFile(...) {
    f = assert(mcLoadFile(...))
    return f()
}

local _mc_loaded = false

fn mcRemoveLoader() {
    if not _mc_loaded {
        return
    }
    loaders = package.loaders or package.searchers
    for i, loader in ipairs(loaders) {
        if loader == mcLoader {
            remove(loaders, i)
            _mc_loaded = false
            return true
        }
    }
    return false    
}

fn mcAppendLoader() {
    if _mc_loaded {
        return
    }
    loaders = package.loaders or package.searchers
    has_loader = false
    for i=1, #loaders {
        if loaders[i] == mcLoader {
            has_loader = true
        }
    }
    if not has_loader {
        _mc_loaded = true
        insert(loaders, mcLoader)
    }
}

fn mcVersion() {
    return "moocscript v0.3.20210605, " .. _VERSION
}

fn mcLoaded() {
    return _mc_loaded
}

-- append loader
mcAppendLoader()

return {
    loadstring : mcLoadString,
    loadfile : mcLoadFile,
    dofile : mcDoFile,
    removeloader : mcRemoveLoader,
    appendloader : mcAppendLoader,
    toAST : toAST,
    toLua : toLua,
    clearProj : clearproj,
    version : mcVersion,
    loaded : mcLoaded
}